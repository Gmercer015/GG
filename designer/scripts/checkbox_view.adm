sheet checkbox_view
{
interface:
    show_layout: false;
    show_colors: false;

    horizontal_alignments: [
        {name: "Default", value: @sentinel_value},
        {name: "align_left", value: @align_left},
        {name: "align_right", value: @align_right},
        {name: "align_center", value: @align_center},
        {name: "align_proportional", value: @align_proportional},
        {name: "align_fill", value: @align_fill}
    ];

    vertical_alignments: [
        {name: "Default", value: @sentinel_value},
        {name: "align_top", value: @align_top},
        {name: "align_bottom", value: @align_bottom},
        {name: "align_center", value: @align_center},
        {name: "align_proportional", value: @align_proportional},
        {name: "align_fill", value: @align_fill}
    ];

    styles: [
        {name: "X-box", value: @xbox},
        {name: "Checkbox", value: @checkbox},
        {name: "Radio Button", value: @radio},
        {name: "Square Button", value: @button},
        {name: "Round Button", value: @round_button}
    ];

    horizontal: @sentinel_value;
    vertical: @sentinel_value;
    indent: 0;
    margin: "";
    name: "";
    alt: "";
    bind: "";
    bind_controller: "";
    bind_view: "";
    value_on: "";
    value_off: "";
    style: @xbox;
    signal_id: "";
    bind_checked_signal: "";
    color: color(127, 127, 127);
    text_color: color(0, 0, 0);
    interior_color: color(0, 0, 0, 0);
    bind_color: "";
    bind_text_color: "";
    bind_interior_color: "";

    indent_valid <== 0 <= indent;
    margin_valid <== valid_margin(margin);
    indent_color <== indent_valid ? color(0, 0, 0) : color(r: 255);
    margin_color <== margin_valid ? color(0, 0, 0) : color(r: 255);
    value_on_valid <== valid_expression(value_on);
    value_on_color <== value_on_valid ? color(0, 0, 0) : color(r: 255);
    value_off_valid <== valid_expression(value_off);
    value_off_color <== value_off_valid ? color(0, 0, 0) : color(r: 255);

    valid <== indent_valid && margin_valid && value_on_valid && value_off_valid;

    results_dict <== {
        horizontal: horizontal,
        vertical: vertical,
        indent: indent != 0 ? indent : @sentinel_value,
        margin: margin != "" ? to_expression(margin) : @sentinel_value,
        name: name != "" ? name : @sentinel_value,
        alt: alt != "" ? alt : @sentinel_value,
        bind: bind != "" ? to_name(bind) : @sentinel_value,
        bind_controller: bind == "" && bind_controller != "" ? to_name(bind_controller) : @sentinel_value,
        bind_view: bind == "" && bind_view != "" ? to_name(bind_view) : @sentinel_value,
        value_on: value_on != "" ? to_expression(value_on) : @sentinel_value,
        value_off: value_off != "" ? to_expression(value_off) : @sentinel_value,
        style: style != @xbox ? style : @sentinel_value,
        signal_id: signal_id != "" ? signal_id : @sentinel_value,
        bind_checked_signal: bind_checked_signal != "" ? to_name(bind_checked_signal) : @sentinel_value,
        color: color != color(127, 127, 127) ? color : @sentinel_value,
        text_color: text_color != color(0, 0, 0) ? text_color : @sentinel_value,
        interior_color: interior_color != color(0, 0, 0, 0) ? interior_color : @sentinel_value,
        bind_color: bind_color != "" ? to_name(bind_color) : @sentinel_value,
        bind_text_color: bind_text_color != "" ? to_name(bind_text_color) : @sentinel_value,
        bind_interior_color: bind_interior_color != "" ? to_name(bind_interior_color) : @sentinel_value
    };

output:
    result <== {value: make_result("checkbox", results_dict)};
}
