layout property_cell
{
    view dialog(placement: place_column)
    {
        popup(name: "Cell Type:",
              bind: @cell_type,
              items: [
                  {name: 'input', value: @input},
                  {name: 'constant', value: @constant},
                  {name: 'logic', value: @logic},
                  {name: 'invariant', value: @invariant},
                  {name: 'interface', value: @interface},
                  {name: 'output', value: @output}
              ]);
        overlay()
        {
            panel(bind: @cell_type, value: @input)
            {
                edit_text(name: "Name:", bind: @cell_name, bind_label_color: @cell_name_color);
                edit_text(name: "Initial Value:", bind: @initial_cell_value, bind_label_color: @initial_cell_value_color); // optional
            }
            panel(bind: @cell_type, value: @constant)
            {
                edit_text(name: "Name:", bind: @cell_name, bind_label_color: @cell_name_color);
                edit_text(name: "Initial Value:", bind: @initial_cell_value, bind_label_color: @initial_cell_value_color);
            }
            panel(bind: @cell_type, value: @logic)
            {
                checkbox(name: "Relate", bind: @logic_cell_relate);
                overlay()
                {
                    panel(bind: @logic_cell_relate, value: true)
                    {
                        edit_text(name: "Condition:",
                                  bind: @relate_cell_condition,
                                  bind_label_color: @relate_cell_condition_color);
                        listbox(name: "Relate clauses:",
                                characters: 20,
                                rows: 5,
                                user_delete: true,
                                hilite_color: color(b: 255),
                                items: @logic_cell_relate_clauses,
                                bind_label_color: @logic_cell_relate_clauses_color);
                        control_button(
                            name: "Add relate clause",
                            bind: @logic_cell_relate_clauses,
                            expression: "append(logic_cell_relate_clauses,"
                                               "{value: size(logic_cell_relate_clauses),"
                                                "type: @relate_clause,"
                                                "state: ['name', '" '"value"' "']})"
                        );
                    }
                    panel(bind: @logic_cell_relate, value: false)
                    {
                        edit_text(name: "Name:", bind: @cell_name, bind_label_color: @cell_name_color);
                        edit_text(name: "Expression:", bind: @cell_define_expression, bind_label_color: @cell_define_expression_color);
                    }
                }
            }
            panel(bind: @cell_type, value: @invariant)
            {
                edit_text(name: "Name:", bind: @cell_name, bind_label_color: @cell_name_color);
                edit_text(name: "Expression:", bind: @cell_define_expression, bind_label_color: @cell_define_expression_color);
            }
            panel(bind: @cell_type, value: @interface)
            {
                edit_text(name: "Name:", bind: @cell_name, bind_label_color: @cell_name_color);
                checkbox(name: "Unlink:", bind: @interface_cell_unlink);
                edit_text(name: "Initial Value:", bind: @initial_cell_value, bind_label_color: @initial_cell_value_color);
                edit_text(name: "Expression:", bind: @cell_define_expression, bind_label_color: @cell_define_expression_color);
            }
            panel(bind: @cell_type, value: @output)
            {
                label(name: "Name: result");
                listbox(name: "Output elements:",
                        characters: 20,
                        rows: 5,
                        user_delete: true,
                        hilite_color: color(b: 255),
                        items: @output_cell_elements,
                        bind_label_color: @output_cell_elements_color);
                control_button(
                    name: "Add output element",
                    bind: @output_cell_elements,
                    expression: "append(output_cell_elements,"
                                       "{value: size(output_cell_elements),"
                                        "type: @output_cell_element,"
                                        "state: ['name', '" '"value"' "']})"
                );
            }
        }

        row(horizontal: align_right)
        {
            button(name: "OK", bind: @result, action: @ok, bind_enabled: @valid);
            button(name: "Apply", action: @apply, bind_enabled: @valid, signal_id: @apply);
            button(name: "Reset", action: @reset);
            button(name: "Cancel", action: @cancel);
        }
    }
}
