layout group_view
{
    view dialog(placement: place_column)
    {
        edit_text(name: "name", bind: @name, alt: 'The text that appears on the group box.');
        edit_text(name: "alt", bind: @alt, alt: 'The tooltip text that appears when the cursor hovers over the group box.');

        reveal(name: "Container Layout Parameters", bind: @show_container_layout);
        optional(bind: @show_container_layout)
        {
            popup(name: "placement", bind: @placement, items: @placements, alt: 'The placement of views within this container.');
            popup(name: "child_horizontal", bind: @child_horizontal, items: @horizontal_alignments, alt: 'The horizontal alignment of views within this container.');
            popup(name: "child_vertical", bind: @child_vertical, items: @vertical_alignments, alt: 'The vertical alignment of views within this container.');
            edit_text(name: "spacing", bind: @spacing, alt: 'An array of numbers to be used for inter-view spacing of contained views, or a single number to use for all spacing.  If the array has more views than the number of contained views, only the applicable ones are used.  If the array has too few views (or a single number is used), the last value is used for all the subsequent view spacing.');
        }

        reveal(name: "Layout Parameters", bind: @show_layout);
        optional(bind: @show_layout)
        {
            popup(name: "horizontal", bind: @horizontal, items: @horizontal_alignments, alt: 'The horizontal alignment of this view in its available space.');
            popup(name: "vertical", bind: @vertical, items: @vertical_alignments, alt: 'The vertical alignment of this view in its available space.');
            edit_number(name: "indent", bind: @indent, alt: 'The horizontal indentation of this view within its available space.  If the view is aligned left, the indentation pushes it right, and vice versa.');
            edit_text(name: "margin", bind: @margin, alt: 'An array of four numbers specifying the margin of space that should be placed around this view (in the order left, top, right, bottom), or a single number for all four values.');
        }

        reveal(name: "Colors", bind: @show_colors);
        optional(bind: @show_colors)
        {
            color_button(name: "color", bind: @color, alt: 'The color in which to render the group box.');
            color_button(name: "text_color", bind: @text_color, alt: "The color in which to render the group's label text.");
            color_button(name: "interior_color", bind: @interior_color, alt: 'The color in which to render the interior of the group box.');
            edit_text(name: "bind_color", bind: @bind_color, alt: 'The name of the Adam cell to which to bind the color in which to render the group box.');
            edit_text(name: "bind_text_color", bind: @bind_text_color, alt: "The name of the Adam cell to which to bind the color in which to render the group's label text.");
            edit_text(name: "bind_interior_color", bind: @bind_interior_color, alt: 'The name of the Adam cell to which to bind the color in which to render the interior of the group box.');
        }
    }
}
