/*
    Copyright 2005-2007 Adobe Systems Incorporated
    Distributed under the MIT License (see accompanying file LICENSE_1_0_0.txt
    or a copy at http://stlab.adobe.com/licenses.html)
*/

layout image_size
{
    view dialog(name: localize("Image Size"), placement: place_row)
    {
        column()
        {
            group(name: localize("Pixel Dimensions"), horizontal: align_fill, placement: place_row)
            {
                column(guide_mask: [ ], horizontal: align_fill, child_horizontal: align_fill)
                {
                    edit_number(name: localize("Width:"), digits: 9, display_disable: true, alt: "Describes the width in pixels",
                                touch: [ @doc_resolution ], bind_units: @pixel_display_units,
                                units: [    { name: localize("pixels"), bind: @dim_width_pixels, format: "#" },
                                            { name: localize("percent"), bind: @dim_width_percent, format: "#.00" }  ]);
        
                    edit_number(name: localize("Height:"), digits: 9, display_disable: true, alt: "Describes the height in pixels",
                                touch: [ @doc_resolution ], bind_units: @pixel_display_units,
                                units: [    { name: localize("pixels"), bind: @dim_height_pixels, format: "#" },
                                            { name: localize("percent"), bind: @dim_height_percent, format: "#.00" }  ]);
                }

                link(bind: @constrain, count: 2, value_on: true, value_off: false);
            }

            group(name: localize("<Document Size:"), horizontal: align_fill, placement: place_row)
            {
                column(guide_mask: [  ], horizontal: align_fill, child_horizontal: align_fill)
                {
                edit_number(name: localize("Width:"), digits: 9, alt: "Set the document width",
                            touch: [ @doc_resolution ],
                            units: [    { name: localize("percent"), bind: @doc_width_percent, format: "#.00" },
                                            { name: localize("inches"), bind: @doc_width_inches, format: "#.000" },
                                            { name: localize("cm"), bind: @doc_width_inches, scale: [2.54, 0.0 ], format: "#.00" },
                                            { name: localize("mm"), bind: @doc_width_inches, scale: [ 25.4, 0.0 ], format: "#.0" },
                                            { name: localize("points"), bind: @doc_width_inches, scale: [ 72, 0 ], format: "#.0" },
                                            { name: localize("picas"), bind: @doc_width_inches, scale: [ 6, 0.0 ], format: "#.00" },
                                            { name: localize("columns"), bind: @doc_width_inches, scale: [ 1, 0.0 ], format: "#.000" }
                                        ]);
                    
                edit_number(name: localize("Height:"), digits: 9, alt: "Set the document height",
                            touch: [ @doc_resolution ],
                            units: [    { name: localize("percent"), bind: @doc_height_percent, format: "#.00" },
                                        { name: localize("inches"), bind: @doc_height_inches, format: "#.000" },
                                        { name: localize("cm"), bind: @doc_height_inches, scale: [2.54, 0.0 ], format: "#.00" },
                                        { name: localize("mm"), bind: @doc_height_inches, scale: [ 25.4, 0.0 ], format: "#.0" },
                                        { name: localize("points"), bind: @doc_height_inches, scale: [ 72, 0 ], format: "#.0" },
                                        { name: localize("picas"), bind: @doc_height_inches, scale: [ 6, 0.0 ], format: "#.00" },
                                        { name: localize("columns"), bind: @doc_height_inches, scale: [ 1, 0.0 ], format: "#.000" }
                                        ]);
    
                edit_number(name: localize("Resolution:"), digits: 9, alt: "Set the document resolution",
                            format: "#.000",
                            touch: [ @dim_width_pixels, @dim_height_pixels ],
                            units: [    { name: localize("pixels/inch"), bind: @doc_resolution },
                                        { name: localize("pixels/cm"), bind: @doc_resolution, scale: [0.393700787, 0.0 ] }
                                    ]);
                }

                overlay(vertical: align_fill)
                {
                    link(bind: @resample, count: 3, value_on: false, value_off: true);
                    link(bind: @constrain, count: 2, value_on: true, value_off: false);
                }
            }
    
            column(guide_balance: [@guide_baseline])
            {
                checkbox(bind: @scale_styles, name: localize("Scale Styles"), alt: "Scale effects proportionally when image is resized");
                checkbox(bind: @constrain, name: localize("Constrain Proportions"), alt: "Constrain aspect ratio");
                row(spacing: 5)
                {
                    checkbox(bind: @resample, name: localize("Resample Image:"), alt: "Interpolate the pixel information");
                    popup(  bind: @resample_method, alt: "Method of pixel interpolation",
                            items: [    { name: localize("Nearest Neighbor"), value: @nearest },
                                            { name: localize("Bilinear"), value: @bilinear },
                                            { name: localize("Bicubic"), value: @bicubic },
                                            { name: localize("Bicubic Smoother"), value: @bicubic_smoother },
                                            { name: localize("Bicubic Sharper"), value: @bicubic_sharper } ]);
                }
            }
        }

        /*

        REVISIT (sparent) : Need to add variables to Eve2 for space_normal, space_small, space_large.

        */

        column(child_horizontal: align_fill)
        {
            button(name: localize("OK"), action: @ok, bind: @result, default: true, alt: "Resize the image with the current settings");
            button( items: [ 
                        { name: localize("Cancel"), action: @cancel, alt: "Do not resize the image and close the dialog" },
                        { name: localize("Reset"), action: @reset, modifiers: @opt, alt: "Reset the dialog settings" }
                     ]);

            button(name: localize("Auto..."), action: @dialog, value: "image_size_auto.eve", alt: "Auto-calculate image resolution");
        }
    }
}
