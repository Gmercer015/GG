/*
    Copyright 2005-2007 Adobe Systems Incorporated
    Distributed under the MIT License (see accompanying file LICENSE_1_0_0.txt
    or a copy at http://stlab.adobe.com/licenses.html)
*/

layout image_size_auto
{
    view dialog(name: localize("Auto Resolution"))
    {
        column()
        {
            edit_number(name: localize("Screen:"), digits: 9, touch: [ @dim_width_pixels, @dim_height_pixels ], 
                        units: [    { name: localize("lines/inch"), bind: @screen_lpi, format: "#.000" },
                                    { name: localize("lines/cm"), bind: @screen_lpi, scale: [2.54, 0.0], format: "#.00" }
                                     ]);
    
            group(name: localize("Quality"), horizontal: align_fill, placement: place_row, child_horizontal: align_proportional)
            { 
                radio_button(touch: [ @screen_lpi, @dim_width_pixels, @dim_height_pixels ], bind: @auto_quality, name: localize("Draft"), value: @draft);
                radio_button(touch: [ @screen_lpi, @dim_width_pixels, @dim_height_pixels ], bind: @auto_quality, name: localize("Good"), value: @good);
                radio_button(touch: [ @screen_lpi, @dim_width_pixels, @dim_height_pixels ], bind: @auto_quality, name: localize("Best"), value: @best);
            }
        }
    
        column(child_horizontal: align_fill)
        {
            button(name: localize("OK"), bind: @result, action: @ok, default: true);
            button(name: localize("Cancel"), action: @cancel);
        }
    }
}
